label brief: # Бриф

    with Fade(0.5, 0.5, 0.5)

    $ mouse_parallax.set((-10, -5, "l0"), (-20, -5, "l1"), (-40, -10, "l2"))
    $ showp("stars back", "stars front_1", "stars front") 
    with eyeopen

    '''

    {fi=[0]-[2]-[0]}С выходом первого человека в космос был проделан \nогромный путь в попытке покорить звёзды.{/fi}

    {fi=[0]-[2]-[0]}Мы шли семимильными шагами, но космос оставался непостижим, \nвсё также холоден и далёк.{/fi}

    {fi=[0]-[2]-[0]}Каждый запуск нашего космического корабля это \nогромная подготовка.{/fi}

    {fi=[0]-[2]-[0]}При этом же{/fi} {bt=h1-p0.5-s0.5}прыжок в неизвестность{/bt}, {fi=[0]-[0.7]-[0]}ведь космос слишком огромен...{/fi}

    {fi=[0]-[2]-[0]}В каждой миссии мы изучали не космос, мы изучали себя \nв попытке осмыслить, навсегда ли наше одиночество во вселенной.{/fi}

    {fi=[0]-[2]-[0]}Увы...{/fi} 

    {fi=[0]-[2]-[0]}Наши корабли были слишком отсталыми, чтобы \nвылететь за пределы внешнего кольца планет.{/fi} 

    '''
    # scene Радиотелескопы (их антены) в заснеженных горах

    # scene Планета Земля к которой подлетает инопланетный космический корабль в форме медузы
    
    '''

    {fi=[0]-[2]-[0]}Раньше все хотели в космос...{/fi}
    
    {fi=[0]-[2]-[0]}Но космос...{/fi}

    {fi=[0]-[2]-[0]}пришёл к нам сам...{/fi}
    '''
    $ scenep()

    

label prologue_01: # Пробуждение
    with ImageDissolve("transitions/01.webp", 4.0, 50)
    #  Темная комната, силуэты металлических шкафов
    show bg black
    pause(1.5)
    hide bg black

    player_base '''
    {fi=[20]-[3]-[20]}Мг-мм-мм... А-а-а…?{/fi}
    
    {fi=[20]-[3]-[20]}Где я? 
    {fi=[0]-[1]-[0]}Как же болит голова!{/fi} 
    
    {fi=[0]-[1]-[0]}Ничего не помню. Как я сюда попал?{/fi}
    ''' with dissolve

    $ light_on = False
    $ check_ticket = False
    $ exit = False
    while not exit:
        menu:
            "Проверить карманы" if not check_ticket:
                $ renpy.block_rollback()
                if light_on:
                    "Вы достаете билет"
                    player_base "{fi=[0]-[1]-[0]}Кажется я начинаю вспоминать...{/fi}"
                    player_base "Точно!" with vpunch # Испольщовать тряску
                    "Воспоминания ударили словно лавина! {fi=[0]-[1]-[0]}Кажется у вас опять начинает болеть голова...{/fi}"
                    player_base "Это же билет!"
                    "Вы вспоминаете как выиграли круиз на корабле этих инопланетян похожих на медуз, заполонивших города."
                    $ exit = True
                else:
                    "Вы нащупываете какой-то листок бумаги у себя в кармане"
                    player_base "{fi=[0]-[1]-[0]}Черт, ничего не видно!{/fi}"
                    "Надо найти выключатель, чтобы разобрать что здесь написано. Положу пока обратно."
                    $ check_ticket = True
            "Нащупать выключатель" if not light_on:
                $ light_on = True
                $ check_ticket = False
                $ renpy.block_rollback()
                "Вы находите выключатель и белый свет ламп бьет вам в глаза"
                player_base "{glitch=20}Агъх.. черт!{/glitch}"
                # Вспышка света - добавить
                '''
                Ваши глаза постепенно начали привыкать к свету...
                \n{w=0.2}Вы наблюдаете странные и подозрительные шкафы \nвокруг вас.
                '''
                #show Свет включен, комната представляет из себя комнату морга
                player_base "{glitch=20}Черт возьми...{/glitch}"
                player_base "{fi=[0]-[1]-[0]}Как я сюда попал? Ничерта не помню{/fi} {glitch=20}угх-х-х... голова...{glitch=20}"
    
    jump prologue_flashback

label prologue_flashback: # Воспоминания о том как прилетели пришельцы 

    show bg white
    with ImageDissolve("transitions/005.webp", 7.0, 80)
        #show Фон: Город с небоскребами и футуристическими зданиями, по улице двигаются люди/машины
    "Наши ученые искали ответ, одни ли мы во вселенной. И спустя много лет поисков, ответ был найден…"

    #show Фон: Город с небоскребами и футуристическими зданиями, по улице двигаются люди/машины, 
    #а также инопланетяне в виде котомедузок и их инопланетные летающие машины

    "Ученые послали в космос сигнал, и на сигнал откликнулись эти существа.
    Пришельцы заполонили всё вокруг, мы их стали называть котомедузками из-за их формы. 
    Они делились с нами технологиями, едой, своей культурой и музыкой…"

    "Город с небоскребами и футуристическими зданиями, 
    по улице двигаются котомедузки в виде промоутеров раздают лотерейные билеты на круиз"

    #NVL
    "Чтобы рассказать людям о себе ещё больше, котомедузки даже решили провести лотерею для счастливчиков. 
    Главный приз - круиз на их космическом корабле. 
    Пришельцы раздавали свои билеты на улицах, отправляли билеты почтой, чтобы приобщить нас к свой “высокой культуре”, 
    удивительно, как быстро они смогли выучить абсолютно все языки народов Земли и войти со своей модой в каждый дом. 
    Люди стали фанатеть от пришельцев."

label prologue_02:
    # Show Фон: Свет включен, комната представляет из себя комнату морга
    # Show Билет
    # play music
    "Но всё же мне ещё непонятно как я оказался здесь…"

    $ check_pockets = False
    $ exit = False
    while not exit:
        menu:
            "Проверить карманы" if not check_pockets:
                $ renpy.block_rollback()
                "В карманах теперь пусто, положу пока билет обратно в жилетку"
                $ check_pockets = True
            "Осмотреться":
                $ exit = True
                $ check_ticket = False
                $ renpy.block_rollback()
                "Это определённо морг. Не знаю, был ли я здесь раньше - я не помню, голова трещит… Что это там..."
